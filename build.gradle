// vim: ts=2 sw=2:
apply plugin: 'idea'
defaultTasks 'clean', 'compileGroovy', 'jar'

subprojects {
  apply plugin: 'groovy'
  apply plugin: 'osgi'
  apply plugin: 'idea'
  project.ext {
    ENV       = System.env
    sourceDir = ENV['PLUGIN_SOURCE_DIR'] ?: 'src'
    testDir   = ENV['PLUGIN_TEST_DIR']   ?: 'test'
    buildDir  = ENV['PLUGIN_BUILD_DIR']   ?: 'build'
  }
  repositories {
    mavenCentral()
    maven {
      url 'http://code.cytoscape.org/nexus/content/repositories/releases/'
    }
  }
  configurations {
    provided
  }
  sourceSets {
    main { compileClasspath += configurations.provided }
  }
  idea {
    module {
      scopes.PROVIDED.plus += configurations.provided
    }
  }
  sourceSets {
    main {
      groovy { srcDirs = [project.ext.sourceDir] }
      resources { srcDir 'resources' }
      output.classesDir = "$project.ext.buildDir/classes"
      output.resourcesDir = "$project.ext.buildDir/resources"
    }
    test {
      groovy { srcDirs = [project.ext.testDir] }
      resources { srcDir 'test-resources' }
      output.classesDir = "$project.ext.buildDir/test-classes"
    }
  }
}

project('module_web_api') {
  dependencies {
    compile files(
      "$rootDir/libs/groovy-all-2.1.6.jar",
      "$rootDir/libs/groovy-wslite-0.8.0.jar",
      "$rootDir/libs/gbench-0.4.2-groovy-2.1.jar")
    provided files(
      "$rootDir/libs/service-api-3.0.1.jar",
      "$rootDir/libs/swing-application-api-3.0.1.jar",
      "$rootDir/libs/org.osgi.core-4.2.0.jar",
      "$rootDir/libs/org.osgi.enterprise-4.2.0.jar",
      "$rootDir/libs/slf4j-api-1.7.5.jar")
    testCompile files(
      "$rootDir/libs/junit-4.11.jar")
  }

  jar {
    manifest {
      classesDir = sourceSets.main.output.classesDir
      classpath = sourceSets.main.runtimeClasspath
      version = '1.0.0'
      name = 'OpenBEL Ws Client'
      symbolicName = 'org.openbel.ws'
      instruction 'Import-Package', '*, wslite.soap, groovy.lang, sun.reflect'
      instruction 'Export-Package', 'org.openbel.ws.api'
    }
  }
}

project('module_main') {
  dependencies {
    compile files(
      "$rootDir/libs/groovy-all-2.1.6.jar")
    provided files(
      "$rootDir/libs/service-api-3.0.1.jar",
      "$rootDir/libs/swing-application-api-3.0.1.jar",
      "$rootDir/libs/org.osgi.core-4.2.0.jar",
      "$rootDir/libs/org.osgi.enterprise-4.2.0.jar",
      "$rootDir/libs/slf4j-api-1.7.5.jar")
    testCompile files(
      "$rootDir/libs/junit-4.11.jar")
  }

  jar {
    manifest {
      classesDir = sourceSets.main.output.classesDir
      classpath = sourceSets.main.runtimeClasspath
      version = '1.0.0'
      name = 'OpenBEL Plugin'
      symbolicName = 'org.openbel.plugin'
      instruction 'Import-Package', '*, groovy.lang, sun.reflect'
      instruction 'Bundle-Activator', 'org.openbel.plugin.Activator'
      instruction 'Export-Package', ''
    }
  }
}
